#!/usr/bin/env fish

if not set -q dots_path
  echo "please set you dots path with stap -p dots_path"
  exit
end

# defaults
set default_name "gruvbox"
set default_variant "light"
set theme_dir "$dots_path/themes"

# initialize defaults only if variables are not set, meaning first script run
if not set -q theme_name
    set -U theme_name $default_name
end
if not set -q theme_variant
    set -U theme_variant $default_variant
end

# fn to check if theme exists
function theme_exists
    set -l name $argv[1]
    set -l variant $argv[2]
    test -e "$theme_dir/$name-$variant"
end

function set_theme
  stow --dotfiles --dir $dots_path/themes --target ~ -D $theme_previous -S $theme
  kill -SIGUSR1 (pgrep kitty)
  if type -q dconf; dconf write /org/gnome/desktop/interface/color-scheme "'prefer-$theme_variant'"; end
  if type -q matugen && test -e ~/.wallpaper; matugen image ~/.wallpaper -m $theme_variant -q; end
end

set local_name $theme_name
set local_variant $theme_variant

switch (count $argv)
    case 1
        set arg $argv[1]
        if test $arg = "light" -o $arg = "dark"
            set local_variant $arg
        else
            set local_name $arg
        end
    case 2
        set local_name $argv[1]
        set local_variant $argv[2]
    case '*'
        echo "Usage: change-theme [theme|variant] [variant]"
        exit 1
end

# validate theme
if theme_exists $local_name $local_variant
    set -U theme_previous $theme

    set -U theme_name $local_name
    set -U theme_variant $local_variant
    set -U theme "$local_name-$local_variant"

    set_theme
    echo "theme set to: $theme"
else
    echo "Error: theme '$local_name-$local_variant' does not exist in $theme_dir"
    exit 1
end

