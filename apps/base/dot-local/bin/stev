#!/usr/bin/env fish

# check if $dots_path is set
if not set -q dots_path
    echo "No dotfiles path given, defaulting to ~/.dotfiles"
    set -U dots_path $HOME/.dotfiles
    exit
end

# functions
function print_help
    echo "Description: Simple fish script wrapping stow for evolve's dotfiles"
    echo ""
    echo "Usage: stev waybar neovim"
    echo ""
    echo "Options:"
    echo "  -h, --help            Show this help message and exit"
    echo "  -l, --list            List all available packages"
    echo "  -p, --path [PATH]       PATH to your dotfiles directory, default to ~/.dotfiles"
    echo "  -g, --grep PATTERN    Search packages name corresponding to pattern"
    echo ""
    echo "Modifiers:"
    echo "  stev -S package       Stow package"
    echo "  stev -D package       Unstow package"
    echo "  stev -R package       Restow package (default)"
    echo ""
    echo "Examples:"
    echo "  # set dotfiles path"
    echo "  stev -p ~/.dotfiles"
    echo "  # enable some package"
    echo "  stev base hyprland gtk kitty"
    echo ""
    echo "  # stev is just wrapper, it mimics stow behavior: you can use several -S -D -R option in the same command"
    echo "  stev waybar -D waybar-square -R gtk"
end

function print_apps
    ls "$dots_path/apps" -1 --indicator-style=none
end

# check args number
if test (count $argv) -lt 1
    echo "Usage: stev app1 app2 ..."
    exit
end

# check for options
set -l i 1
while test $i -le (count $argv)
    set current_arg $argv[$i]
    set -l next_index (math $i + 1)

    if test $current_arg = -p || test $current_arg = --path
        if test $next_index -gt (count $argv)
            echo "current path: $dots_path"
            exit
        end
        # set path (format and convert)
        set -U dots_path (realpath (string replace -r '^~' $HOME $argv[$next_index]))
        exit

    else if test $current_arg = -h || test $current_arg = --help
        print_help
        exit

    else if test $current_arg = -l || test $current_arg = --list
        print_apps
        exit

        # try to use ripgrep, fallback to grep if unavaible
    else if test $current_arg = -g || test $current_arg = --grep
        if type -q rg
            print_apps | rg $argv[$next_index]
        else
            print_apps | grep $argv[$next_index]
        end
        exit

        # if the script has not exited yet it means it is a package or a modifier
    else
        set packages $packages $current_arg
    end

    set i (math $i + 1)
end

# apply stow
stow --dotfiles --dir "$dots_path/apps" --target ~ -R $packages

## breakdown
# `--dotfiles` because paths start with 'dot-' instead of '.'
# `--dir` for the source dir
# '--target ~' because we target the home dir, not directly .config or .local,
#       which is what allows for such a modular yet simple setup, stow trully is amazing
#  `-R` default to restow to get that precious 0.1 second not waster on writing -R
#  `$packages` and finally the list of packages and modifiers
