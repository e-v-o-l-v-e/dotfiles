#!/usr/bin/env fish

if not set -q dots_path
    echo "please set your dotfiles's path with stev -p dots_path"
    exit 1
end

### defaults
set -l DEFAULT_NAME gruvbox
set -l DEFAULT_VARIANT light
set -l DEFAULT_WP_MODE never

set -g theme_dir "$dots_path/themes"
set -g use_matugen false

### persistent state init
set -q theme_name || set -U theme_name $DEFAULT_NAME
set -q theme_variant || set -U theme_variant $DEFAULT_VARIANT
set -q wallpaper_mode || set -U wallpaper_mode $DEFAULT_WP_MODE
set -q wall_dir || begin
    if test -e $HOME/Pictures/Wallpapers
        set -U wall_dir $HOME/Pictures/Wallpapers
        echo "No wallpaper directory defined, defaulting to ~/Pictures/Wallpapers/"
    else
        echo "No wallpaper directory defined, and ~/Pictures/Wallpapers/ doesn't exist, please set the wallpaper directory with stev-theme --wall-dir"
    end
end

### functions
function theme_exists
    # $argv is the list of args passed to the fonction, not the script
    test -e "$theme_dir/$argv[1]-$argv[2]"
end

function list_themes
    for dir in $theme_dir/*
        path basename $dir | string replace -r '\-light$|\-dark$' ""
    end | sort | uniq
end

function pick_wallpaper
    if test $wallpaper_mode = "image"
        echo $image
    else
        set -l files $wall_dir/$theme_name/*
        if test (count $files) -gt 0
            random choice $files
        else
            echo "$HOME/.wallpaper"
        end
    end
end

### actions
function apply_wallpaper
    set -l wp (pick_wallpaper)

    if type -q waypaper
        waypaper --folder "$wall_dir/$theme_name" --wallpaper $wp &>/dev/null
    else
        type -q swww and swww img $wp &>/dev/null
        type -q plasma-apply-wallpaperimage; and plasma-apply-wallpaperimage $wp &>/dev/null
    end

    test $wallpaper_mode = once; and set -U wallpaper_mode never
end

function set_nvim_theme
    set -U nvim_theme $argv[1]

    for sock in (fd "nvf|nvim" /run/user/(id -u)/)
        nvim --server $sock --remote-send ":colorscheme $nvim_theme<cr>"
    end
end

function apply_theme
    stow --dotfiles --dir $dots_path/themes --target ~ -D $theme_previous -S $theme

    kill -SIGUSR1 (pgrep kitty) &>/dev/null

    set_nvim_theme $theme_name

    type -q dconf; and dconf write /org/gnome/desktop/interface/color-scheme "'prefer-$theme_variant'"

    $use_matugen; and type -q matugen; and test -e ~/.wallpaper; and matugen image ~/.wallpaper -m $theme_variant -q &>/dev/null

    apply_wallpaper
end

### help
function print_help
    echo "Usage: stev-theme [theme] [variant] [options]"
    echo "  (order doesn't matter)"
    echo
    echo "Arguments:"
    echo "  theme                 Theme name (e.g. gruvbox, catppuccin)"
    echo "  variant               Theme variant (light or dark)"
    echo
    echo "stev-theme will respect your current theme/variant if you don't specify one"
    echo
    echo "Options:"
    echo "  -h, --help                  Show this help message and exit"
    echo "  -l, --list                  Show available themes and exit"
    echo "  -w, --wallpaper-mode MODE   Set wallpaper behavior:"
    echo "                       never      do not change wallpaper, now and on future theme changes"
    echo "                       once       change wallpaper just this time, but not on future theme changes"
    echo "                       always     change wallpaper now and on subsequents theme change or calls to"
    echo "                                  stev-theme without argument"
    echo "  -i, --image FILE            set FILE as wallpaper and wallpaper_mode to \"never\""
    echo "                                  /!\ do no use with `-w` as it will take precedence"
    echo "  --wall-dir DIR              set DIR as base wallpapers directory"
    echo "  -r, --reset-waypaper        reset waypaper's folder"
    echo
    echo "WARNING"
    echo "For the correctly themed wallpaper change to work the wallpapers must be sorted in"
    echo "subfolder named identically to the theme in \$dots_path/themes, without the variant."
    echo "if that is not your case you should disable it with `stev-theme -w never` and rely"
    echo "on the -i option, or just waypaper"
    echo
    echo "Examples:"
    echo "  stev-theme gruvbox dark"
    echo "  stev-theme catppuccin -w once"
    echo "  stev-theme -w always"
    echo
end

### simple wallpaper change if no args
if test (count $argv) -eq 0
    if test $wallpaper_mode = never
        print_help
        exit 1
    else
        apply_wallpaper
    end
    echo exiting now
    exit 0
end

### argument parsing
set -l name $theme_name
set -l variant $theme_variant
set -l i 1

while test $i -le (count $argv)
    set -l arg $argv[$i]
    set -l next $argv[(math $i + 1)]

    switch $arg
        case -h --help
            print_help
            exit
        case -l --list
            list_themes
            exit

        case --wall-dir
            test -d "$next"; and set -U wall_dir "$next"
            set i (math $i + 1) # Skip the directory argument

        case -w --wallpaper-mode
            if not string match -qr '^(never|once|always)$' -- "$next"
                echo "option `-w` requires one of the following"
                echo "   never   # do not change the wallpaper, now or never"
                echo "   once    # change wallpaper just this time"
                echo "   always  # always change wallpaper"
                exit
            else
                set -U wallpaper_mode "$next"
                set i (math $i + 1) # Skip the mode argument
                apply_wallpaper 
            end

        case -f --full-name
            set parts (string split '-' "$next")
            set name $parts[1]
            set variant $parts[2]
            set i (math $i + 1) # Skip the name argument

        case -i --image
            set -g image $next
            set wallpaper_mode "image"
            apply_wallpaper
            set wallpaper_mode "never"
            set i (math $i + 1) # Skip the file argument

        case -r --reset-waypaper
            type -q waypaper; and waypaper --folder "$wall_dir" --wallpaper "$HOME/.wallpaper" &>/dev/null

        case light dark
            set variant $arg

        case '*'
            set name $arg
    end
    set i (math $i + 1)
end

### apply theme
if not theme_exists $name $variant
    echo "Error: theme '$name-$variant' does not exist in $theme_dir"
    exit 1
end

test "$name-$variant" = "$theme"; and exit

set -q theme; and set -U theme_previous $theme
set -U theme_name $name
set -U theme_variant $variant
set -U theme "$name-$variant"

apply_theme

echo "theme set to: $theme"
type -q swww && \
    notify-send -i (swww query | sed -n 's/.*image: \(.*\)$/\1/p') Theming "Switched to $theme"; exit
notify-send Theming "Switched to $theme"
